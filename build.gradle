plugins {
    id "java"
    id "org.springframework.boot" version "2.1.2.RELEASE"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
}

// Version config
ext {
    spring_cloud_version = "Greenwich.RELEASE"
    spring_cloud_feign_version = "1.4.6.RELEASE"
    spring_boot_version = "2.1.4.RELEASE"
    springframework_version = "5.1.5.RELEASE"
    mybatis_spring_boot_version = "2.0.0"
    mybatis_version = "3.5.0"
    pagehelper_spring_boot_version = "1.2.10"
    druid_version = "1.1.14"
    mysql_connector_java_version = "8.0.15"
    commons_io_version = "2.6"
    org_apache_commons_io_version = "1.3.2"
    commons_fileupload_version = "1.4"
    commons_codec_version = "1.12"
    commons_lang3_version = "3.8.1"
    okhttp_version = "3.13.1"
    // feign_okhttp_version = "8.18.0"
    // Github version
    feign_okhttp_version = "10.2.0"
    lombok_version = "1.18.6"
    springfox_swagger_version = "2.9.2"
    fastjson_version = "1.2.56"
    fastdfs_client_java_version = "1.27.0.0"
    elasticsearch_version = "6.6.2"
    guava_version = "27.1-jre"

    httpclient_version = "4.5.8"
    zip4j_version = "1.3.2"
    spring_security_jwt_version = "1.0.10.RELEASE"

    spring_data_commons_version = "2.1.4.RELEASE"
    spring_data_mongodb_version = "2.1.4.RELEASE"
    hibernate_version = "5.4.2.Final"
}

// Sub module config
allprojects {
    apply plugin: "java"
    group "cat.wars.cms"
    version "1.0-SNAPSHOT"

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        jcenter()
        mavenCentral()
    }
}

// Config
subprojects {
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    def defaultEncoding = "UTF-8"
    tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }

    javadoc {
        options.encoding = defaultEncoding
        options.addBooleanOption("Xdoclint:none", true)
    }

    compileJava.dependsOn(processResources)

    test {
        include "**/model/**", "**/controller/**", "**/usecase/**", "**/util/**", "**/context/**"
    }

    springBoot {
        buildInfo()
    }

    // Version ctl
    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${spring_cloud_version}"
        }
        dependencies {
            dependency "org.projectlombok:lombok:${lombok_version}"
            dependency "io.springfox:springfox-swagger2:${springfox_swagger_version}"
            dependency "io.springfox:springfox-swagger-ui:${springfox_swagger_version}"
            dependency "mysql:mysql-connector-java:${mysql_connector_java_version}"
            dependency "org.mybatis.spring.boot:mybatis-spring-boot-starter:${mybatis_spring_boot_version}"
            dependency "com.github.pagehelper:pagehelper-spring-boot-starter:${pagehelper_spring_boot_version}"
            dependency "com.alibaba:druid:${druid_version}"
            dependency "com.squareup.okhttp3:okhttp:${okhttp_version}"
            // dependency "com.netflix.feign:feign-okhttp:${feign_okhttp_version}"
            dependency "io.github.openfeign:feign-okhttp:${feign_okhttp_version}"
            dependency "commons-io:commons-io:${commons_io_version}"
            dependency "org.apache.commons:commons-io:${org_apache_commons_io_version}"
            dependency "commons-fileupload:commons-fileupload:${commons_fileupload_version}"
            dependency "commons-codec:commons-codec:${commons_codec_version}"
            dependency "org.apache.commons:commons-lang3:${commons_lang3_version}"
            dependency "com.alibaba:fastjson:${fastjson_version}"
            dependency "net.oschina.zcx7878:fastdfs-client-java:${fastdfs_client_java_version}"
            dependency "org.elasticsearch.client:elasticsearch-rest-high-level-client:${elasticsearch_version}"
            dependency "org.elasticsearch:elasticsearch:${elasticsearch_version}"
            dependency "com.google.guava:guava:${guava_version}"
        }
    }

    // Dependencies ctl
    dependencies {
//        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${spring_boot_version}"
//        compileOnly "org.springframework.boot:spring-boot-configuration-processor:${spring_boot_version}"
    }
}

// Sub module depend
project("cms-model") {
    dependencies {
        implementation project(":cms-common")
        implementation project(":cms-utils")
    }
}
project("cms-api") {
    dependencies {
        implementation project(":cms-common")
        implementation project(":cms-model")
    }
}
project("cms-service-manager") {
    dependencies {
        implementation project(":cms-common")
        implementation project(":cms-model")
        implementation project(":cms-utils")
        implementation project(":cms-api")
    }
}
project("cms-service-manager-client") {
    dependencies {
        implementation project(":cms-model")
    }
}
project("cms-service-manager-course") {
    dependencies {
        implementation project(":cms-common")
        implementation project(":cms-model")
        implementation project(":cms-utils")
        implementation project(":cms-api")
    }
}

// Warpper
wrapper {
    gradleVersion = "5.2.1"
}
